cmake_minimum_required(VERSION 2.8)

### PROJECT VERSION

project(GetSet CXX)
set (GetSet_VERSION_MAJOR 0)
set (GetSet_VERSION_MINOR 9)

### EXTERNAL DEPENDENCIES

find_package(Qt4 REQUIRED)

include(${QT_USE_FILE})
add_definitions(${QT_DEFINITIONS})

include_directories("${PROJECT_SOURCE_DIR}")

### CLI EXECUTABLE

# GetSetCLI: tool to create a GUI from an xml-file to configure command line apps
add_executable(GetSetCLI
	main.cpp
)
add_dependencies(GetSetCLI GetSet GetSetGui)
target_link_libraries(GetSetCLI GetSet GetSetGui ${QT_LIBRARIES})

# An example using GetSet for command line input, no Qt needed. GUI can be generated with GetSetCLI.
add_executable(CLITestApp
	main_cli.cpp
)
add_dependencies(CLITestApp GetSet GetSetGui)
target_link_libraries(CLITestApp GetSet)

# GetSet and GetSetGui Libraries
add_subdirectory(./GetSet "${CMAKE_CURRENT_BINARY_DIR}/GetSet")
add_subdirectory(./GetSetGui "${CMAKE_CURRENT_BINARY_DIR}/GetSetGui")

# Define install location for the CLI destinations
install(TARGETS GetSetCLI EXPORT GetSetDepends RUNTIME DESTINATION "${CMAKE_INSTALL_PREFIX}/bin")  

### INSTALLER

# Build a CPack driven installer package
include (InstallRequiredSystemLibraries)
set (CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/License.txt")
set (CPACK_PACKAGE_VERSION_MAJOR ${GetSet_VERSION_MAJOR})
set (CPACK_PACKAGE_VERSION_MINOR ${GetSet_VERSION_MINOR})
include (CPack)

### SOURCE DISTRIBUTION (find_package)

# Add all targets to the build-tree export set
export(TARGETS GetSet GetSetGui GetSetCLI FILE "${PROJECT_BINARY_DIR}/GetSetDepends.cmake")
# This registers the build-tree with a global CMake-registry
export(PACKAGE GetSet)

# Configure build tree paths and version
configure_file(GetSetBuildTreeSettings.cmake.in "${PROJECT_BINARY_DIR}/GetSetBuildTreeSettings.cmake" @ONLY)
configure_file(GetSetConfigVersion.cmake.in "${PROJECT_BINARY_DIR}/GetSetConfigVersion.cmake" @ONLY)

# Install the export set for use with the install-tree (creates .cmake files in installation)
install(EXPORT GetSetDepends DESTINATION "${CMAKE_INSTALL_PREFIX}" COMPONENT dev)
install(FILES "${PROJECT_BINARY_DIR}/GetSetConfig.cmake" DESTINATION "${CMAKE_INSTALL_PREFIX}" COMPONENT dev)
